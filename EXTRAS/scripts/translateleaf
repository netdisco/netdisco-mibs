#!/usr/bin/env perl
# translateleaf [vendor] leafname - run snmptranslate on MIBs and show output
#Â no param will load all MIBs.

use strict;
use warnings;

use charnames ':full';
binmode STDOUT, ':utf8';
$|++;

use Time::HiRes 'sleep';
use File::Spec::Functions qw(splitdir catfile);
use Term::ANSIColor qw(:constants);
use POSIX qw(:sys_wait_h);

use FindBin;
use lib catfile($FindBin::Bin, 'lib');
use Helpers;

# TODO: arguably rfc:net-snmp should be enough!
# but it seems cisco, nortel, etc are required. maybe in the future, fix this.
my @mibdirs = sort grep { -d and $_ !~ m#/EXTRAS$# } glob("$ENV{MIBHOME}/*");
$ENV{MIBDIRS} = join ':', @mibdirs;

# load mib_index2.txt mapping all current known MIBs
my (undef, undef, $mibs_for) = parse_index2();

my $vendor = $ARGV[0];
my $oid = $ARGV[1];

if ($vendor and not $oid) { $oid = $vendor; $vendor = undef; }
die "error: oid arg is required" unless $oid;

if ($vendor and !exists $mibs_for->{$vendor}) {
  print "error: vendor arg must exist in MIBHOME\n";
  exit(1);
}
$vendor ||= 'all';

my $mibs = ($vendor eq 'all') ? 'ALL' : (join ':', sort @{$mibs_for->{$vendor}});
print CYAN, "\N{ALMOST EQUAL TO} translating for '$vendor'", CLEAR "\n";

defined(my $pid = fork) or die "Couldn't fork: $!";
if (!$pid) { # Child
  exec(qq{snmptranslate -IR -Le -m '$mibs' $oid})
    or die "Couldn't exec: $!";
} else { # Parent
  while (! waitpid($pid, WNOHANG)) {
    sleep 0.05;
  }
}

print GREEN, "\N{BLACK FLAG} Translate done.", CLEAR, "\n";
exit(0);
